Функция ТаблицаЗначенийИзCSVФайла(ИмяФайла, Разделитель = ";", ЗаголовкиИзПервойСтроки = Ложь) Экспорт
	
	Текст = Новый ЧтениеТекста(ИмяФайла);
	СимволКавычки = Символ(34);
	Результат = Новый ТаблицаЗначений;
	ТекСтрока = Текст.ПрочитатьСтроку(); 
	
	Если ТекСтрока <> Неопределено Тогда
		
		МассивЗначений = СтрРазделить(ТекСтрока, Разделитель);
		ИндексКолонки = 0;
		
		Для Каждого ИмяКолонки Из МассивЗначений Цикл
			ИмяКолонки = ?(НЕ ЗаголовкиИзПервойСтроки, "Кол" + ИндексКолонки,
			УбратьЛишниеСимволы(ИмяКолонки));
			Результат.Колонки.Добавить(ИмяКолонки);
			ИндексКолонки = ИндексКолонки + 1;
		КонецЦикла; 
		
		Если ЗаголовкиИзПервойСтроки Тогда
			ТекСтрока = Текст.ПрочитатьСтроку();
		КонецЕсли;
		
	КонецЕсли;
	
	Пока ТекСтрока <> Неопределено Цикл // строки читаются до символа перевода строки
		
		НоваяСтрока = Результат.Добавить();	
		МассивЗначений = СтрРазделить(ТекСтрока, Разделитель);
		ИндексКолонки = 0;
		
		Для Каждого ТекЗначение Из МассивЗначений Цикл
			
			Если ТипЗнч(ТекЗначение) = Тип("Строка") и СтрНачинаетсяС(ТекЗначение, СимволКавычки) 
				И СтрЗаканчиваетсяНа(ТекЗначение, СимволКавычки) тогда	
				НоваяСтрока[ИндексКолонки] = Сред(ТекЗначение, 2, СтрДлина(ТекЗначение) - 2);
			Иначе
				НоваяСтрока[ИндексКолонки] = ТекЗначение;
			КонецЕсли;
			
			ИндексКолонки = ИндексКолонки + 1;
		КонецЦикла;
		
		ТекСтрока = Текст.ПрочитатьСтроку();
	КонецЦикла;
	
	Возврат Результат;
КонецФункции  


Функция УбратьЛишниеСимволы(ИмяКолонки)
	
	ИмяКолонки = СтрЗаменить(ИмяКолонки,Символ(34),"");
	ИмяКолонки = СтрЗаменить(ИмяКолонки," ","_");
	ИмяКолонки = СтрЗаменить(ИмяКолонки,Символы.НПП,"_");
	ИмяКолонки = СтрЗаменить(ИмяКолонки,"/","_");
	
	Возврат ИмяКолонки;
	
КонецФункции


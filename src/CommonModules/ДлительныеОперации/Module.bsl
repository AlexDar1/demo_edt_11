Функция НовоеЗадание(ИмяМетода = Неопределено, Параметры = Неопределено, УИДФоновогоЗадания = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Массив;
	КонецЕсли;
	
	Задание = Новый Структура;
	Задание.Вставить("ИмяМетода", ИмяМетода);
	Задание.Вставить("Параметры", Параметры);
	Задание.Вставить("УИДФоновогоЗадания", УИДФоновогоЗадания);
	
	Возврат Задание;
	
КонецФункции

Функция ВыполнитьЗадания(Задания, КоличествоПараллельноВыполняемых, Наименование = "") Экспорт  
	
	МассивЗапущенныхЗаданий = Новый Массив;
	СчФоновых = 0;
	
	ВсеВыполнено = Ложь;
	Пока НЕ ВсеВыполнено цикл
		
		ВсеВыполнено = Истина;
		КоличествоВыполняющихся = 0;
		Для каждого Задание из Задания цикл
			
			Если Задание.УИДФоновогоЗадания = Неопределено тогда
				СчФоновых = СчФоновых + 1;
				ИмяФоновового = Наименование + " " + Строка(СчФоновых);
				
				НовоеФоновое = ФоновыеЗадания.Выполнить(Задание.ИмяМетода, Задание.Параметры,, ИмяФоновового);
				
				МассивЗапущенныхЗаданий.Добавить(НовоеФоновое);
				Задание.УИДФоновогоЗадания = НовоеФоновое.УникальныйИдентификатор;
				КоличествоВыполняющихся = КоличествоВыполняющихся + 1;
				ВсеВыполнено = Ложь;
			Иначе
				Фоновое = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Задание.УИДФоновогоЗадания);
				Если Фоновое <> Неопределено И Фоновое.Состояние = СостояниеФоновогоЗадания.Активно тогда
					КоличествоВыполняющихся = КоличествоВыполняющихся + 1;
					ВсеВыполнено = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если КоличествоВыполняющихся = КоличествоПараллельноВыполняемых тогда
				ВсеВыполнено = Ложь;
				Прервать;
			КонецЕсли;
				
		КонецЦикла;                                                     

		Пауза(5);
		
	КонецЦикла;
	
	Возврат МассивЗапущенныхЗаданий;
	
КонецФункции 

Функция Пауза(Знач Секунды) Экспорт
	Команда = "ping -n " + Формат(Секунды + 1, "ЧДЦ=0; ЧГ=") + " -w 2000 0.0.0.1";
	ЗапуститьПриложение(Команда, , Истина);
	
	Возврат Секунды;
КонецФункции

